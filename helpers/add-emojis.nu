#!/usr/bin/env nu

# Regex for parsing this file: https://www.unicode.org/Public/17.0.0/emoji/emoji-test.txt
# Capture groups:
# | code | Emoji code, can be multiple blocks consisting of 4-5 characters
# | emoji | The emoji, surrounded by spaces |
# | description | a short description of the emoji, always starts with `E` followed by some number code (1.0, 0.2, ...)
let emoji_regex = '(?P<code>(^[(A-Z0-9)\s]{4,})[^;])(.*-qualified\s+#)(?P<emoji>[\s].*)?(?P<description>E\d\.\d.+)'

# The place where the list of emojis is published,
# should revisit this when a new version of Emojis is released
let emoji_list_url = 'https://www.unicode.org/Public/17.0.0/emoji/emoji-test.txt'
# What this does:
# 1. Get the content from the URL
# Parse it using the regex above
# Select the 3 capture groups from the parsing result
# trim all strings
# convert to json
# save to file, using  `-f` to overwrite existing files


http get $emoji_list_url
    | parse --regex $emoji_regex
    | select code emoji description
    | str trim
    | to json
    | save -f data/emojis.json

# Write the rust data file from the json

# get the emojis from the file
let emoji = (open data/emojis.json | get emoji)

# calculate the length
let emoji_length = ($emoji | length | into string)

# prepare the file template
let file_template = r#'/// This data structure provides the list of emoji_length emojis used for the gen_emoji generator
// this file is generated by the ./helpers/add-emojis.nu script

pub const DATA_EMOJIS: [&str; emoji_length] = emoji_place;
'#

# good ol' find and replace, save the file
echo $file_template | str replace -a emoji_length ($emoji_length) | str replace -a emoji_place ($emoji | to json) | save -f src/data/emojis.rs
